#
# Copyright (c) 2017, RTE (http://www.rte-france.com)
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at http://mozilla.org/MPL/2.0/.
#

# query: insertNickName
# GRAPH clause is needed for jena
prefix : <http://example/>
prefix foaf: <http://xmlns.com/foaf/0.1/>
INSERT {GRAPH <contexts:foaf/abc-nicks.ttl> { :bob foaf:nick "BG" }}
WHERE {}

# query: selectNickName
prefix foaf: <http://xmlns.com/foaf/0.1/>
SELECT * {
	GRAPH ?graphPersons
				{ ?x 
                foaf:mailbox ?mbox ;
			    foaf:nick ?nick	}
	}

# query: deleteLastName
#--> OK FOR RDF4J and blazegraph
prefix : <http://example/>
prefix foaf: <http://xmlns.com/foaf/0.1/>
DELETE { ?x foaf:lastName ?y }
WHERE {
	?x foaf:lastName ?y .
	FILTER (?y = "Channing")
}


# query: selectLastName
prefix foaf: <http://xmlns.com/foaf/0.1/>
SELECT * { ?x
              foaf:mailbox ?mbox ;
              foaf:lastName ?lastName
}

# query: updateLastNameGraph
# We need to specify the graph, otherwise insert will be done into default graph.
prefix : <http://example/>
prefix foaf: <http://xmlns.com/foaf/0.1/>
WITH <contexts:foaf/abc-lastNames.ttl> 
DELETE { ?x foaf:lastName ?y }
INSERT {?x foaf:lastName "Grebenshchikov" }
WHERE {	?x foaf:lastName ?y .
	    FILTER (?y = "Marley")
}

# query: updatePersonTwoGraphs
prefix : <http://example/>
prefix foaf: <http://xmlns.com/foaf/0.1/>
DELETE {
		?x foaf:lastName ?y .
 		?x foaf:mailbox ?y .
 		} 
		
INSERT {
 GRAPH <contexts:foaf/abc-lastNames.ttl> {?x foaf:lastName "Cooper" .}
 GRAPH <contexts:foaf/abc-nicks.ttl> {?x foaf:mailbox "mailto:alice@exampleNew" .}
		}
WHERE {
		?x foaf:lastName|foaf:mailbox ?y .
	    FILTER (
	    		(?y = "Liddell" || ?y = <mailto:alice@example>)
	    		)
}

# query: updatePersonTwoGraphsJena
#https://www.w3.org/TR/sparql11-update/#insertData Example 12
prefix : <http://example/>
prefix foaf: <http://xmlns.com/foaf/0.1/>
DELETE {
		GRAPH <contexts:foaf/abc-lastNames.ttl> {?x foaf:lastName ?y .}
 		GRAPH <contexts:foaf/abc-nicks.ttl> {?x foaf:mailbox ?z .}
 		} 
		
INSERT {
 GRAPH <contexts:foaf/abc-lastNames.ttl> {?x foaf:lastName "Cooper" .}
 GRAPH <contexts:foaf/abc-nicks.ttl> {?x foaf:mailbox "mailto:alice@exampleNew" .}
		}
WHERE {
		GRAPH <contexts:foaf/abc-nicks.ttl> {
				?x foaf:mailbox ?z .
	    		FILTER (?z = <mailto:alice@example>)
	    		}
		GRAPH <contexts:foaf/abc-lastNames.ttl> {
				?x foaf:lastName ?y .
				}
}

# query: selectLastNameGraphs
prefix foaf: <http://xmlns.com/foaf/0.1/>
SELECT * {
    { GRAPH ?graphPersons { ?x foaf:mailbox ?mbox } }
    { GRAPH ?graphLastnames { ?x foaf:lastName ?lastName } }
}
			 